apply plugin: 'com.android.library'

android {
    compileSdkVersion rootProject.compileSdkVersion
    buildToolsVersion rootProject.buildToolsVersion

    defaultConfig {
        versionCode 1
        versionName version
        minSdkVersion 8
        consumerProguardFiles 'proguard.txt'
    }

    def scribeEndpointOverride
    if (project.hasProperty('scribeEndpointOverride')) {
        scribeEndpointOverride = project.getProperty('scribeEndpointOverride')
    } else {
        scribeEndpointOverride = ''
    }
    def scribeSequence
    if (project.hasProperty("scribeSequence")) {
        scribeSequence = project.getProperty('scribeSequence')
    } else {
        scribeSequence = ''
    }

    buildTypes {
        debug {
            buildConfigField "String", "GROUP", "\"${pom_group_id}\""
            buildConfigField "String", "ARTIFACT_ID", "\"${pom_artifact_id}\""
            buildConfigField "String", "BUILD_NUMBER", "\"${buildNumber}\""
            buildConfigField "long", "BUILD_TIME", "${System.currentTimeMillis()}L"
            buildConfigField "String", "SCRIBE_ENDPOINT_OVERRIDE", "\"${scribeEndpointOverride}\""
            buildConfigField "String", "SCRIBE_SEQUENCE", "\"${scribeSequence}\""
            // recommended fix from https://code.google.com/p/android/issues/detail?id=61573
            // triggered by including easymock 3.2 in testing/build.gradle
            packagingOptions {
                exclude 'META-INF/LICENSE'
                exclude 'META-INF/NOTICE'
                exclude 'META-INF/INDEX.LIST'
                exclude 'META-INF/jersey-module-version'
                exclude 'META-INF/ASL2.0'
                exclude 'LICENSE'
                exclude 'NOTICE'
                exclude 'asm-license.txt'
            }
        }

        release {
            buildConfigField "String", "GROUP", "\"${pom_group_id}\""
            buildConfigField "String", "ARTIFACT_ID", "\"${pom_artifact_id}\""
            buildConfigField "String", "BUILD_NUMBER", "\"${buildNumber}\""
            buildConfigField "long", "BUILD_TIME", "${System.currentTimeMillis()}L"
            buildConfigField "String", "SCRIBE_ENDPOINT_OVERRIDE", "\"${scribeEndpointOverride}\""
            buildConfigField "String", "SCRIBE_SEQUENCE", "\"${scribeSequence}\""
        }
    }

    lintOptions {
        warningsAsErrors true
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }

    //Gradle publishes only release by default for multi module projects
    //This will publish all build variants and we can specifically choose them
    defaultPublishConfig 'release'
    publishNonDefault true

}

dependencies {

    compile rootProject.fabric
    compile rootProject.retrofit
    compile rootProject.gson

    //Will use the provided packages if they are available in the app developers workspace.
    provided rootProject.okHttp
    provided rootProject.okHttpUrlConnection

    androidTestCompile rootProject.fabricTest
    androidTestCompile easymock
    androidTestCompile (rootProject.junitAddons) {
        transitive = false
    }

    debugCompile rootProject.mockito
    debugCompile rootProject.dexmakerMockito
    debugCompile rootProject.fabricTest
}
